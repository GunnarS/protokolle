set terminal pdf

po = 'data/plot_oszi'
po2 = 'data/plot_oszi2'

call po 'slow/erster/1.txt' 'erster_vor.pdf' 3
call po 'slow/erster/1.txt' 'erster_nach.pdf' 2
call po 'slow/zweiter/1.txt' 'zweiter_vor.pdf' 2
call po 'slow/zweiter/1.txt' 'zweiter_nach.pdf' 3
call po2 'slow/erster/2.txt' 'erster_sca.pdf' 2 3
call po2 'slow/zweiter/2.txt' 'zweiter_sca.pdf' 2 3


set xlabel 'Bin'
set ylabel 'N'

set output 'grafiken/erster_na22.pdf'
plot 'data/slow/erster/3.txt' w impulses notitle
set output 'grafiken/zweiter_na22.pdf'
plot 'data/slow/zweiter/3.txt' w impulses notitle

set output 'grafiken/erster_ba.pdf'
plot [0:500] 'data/slow/erster/5.txt' w impulses notitle
set output 'grafiken/zweiter_ba.pdf'
plot [0:500] 'data/slow/zweiter/5.txt' w impulses notitle

set xlabel 't/ns'
set ylabel 'U/V'

set output 'grafiken/fast_1.pdf'
plot [60:120] 'data/fast.txt' u ($1*1e9):2 w lines notitle
set output 'grafiken/fast_2.pdf'
plot [-80:-40] 'data/fast.txt' u ($1*1e9):2 w lines notitle

# call 'data/fit_peak.plot' 'data/slow/erster/3.txt' 2 503 1 303 1 0

f = 'data/fit_peak.plot'
d = 'data/zeiteichung.txt'

set print 'data/zeiteichung_peaks.txt'
call f d 2 184 10 29 1 0
call f d 2 421 33 29 1 0
call f d 2 662 10 29 1 0
call f d 2 900 10 29 1 0
call f d 2 1135 10 29 1 0
unset print

set output 'grafiken/zeiteichung.pdf'

set xlabel 't/ns'
set ylabel 'N'

f(x) = m * x + b
fit f(x) 'data/zeiteichung_peaks.txt' u (16*$0):1:(1/$4**2) via m, b

plot 'data/zeiteichung_peaks.txt' u (16*$0):1:($4 + (rand(0) - 0.5) * $5) \
    w yerr notitle, f(x) notitle

h(x) = (x-b)/m

lambda = 0.125 
g(x) = -lambda * x + A
fit g(x) 'data/lebensdauer.txt' u ($1>1000&&$1<2000?h($1):1/0):(log($2))\
        via lambda, A


set output 'grafiken/lebensdauer.pdf'
set yrange [100:12000]
#set xrange [-13:-6]
plot 'data/lebensdauer.txt' u (h($1)):2 w impulses notitle, \
    exp(g(x)) w lines lw 2 notitle
