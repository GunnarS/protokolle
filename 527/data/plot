set terminal lua solid

set angles degrees

set xlabel 'Winkel/$^\circ$'
set ylabel 'Intensität'
set xrange [-15:345]

set output 'grafiken/untergrund.tikz'

f = 'data/untergrund.txt'
dy(x) = sqrt(x)
text = "%s, %s Tag"
plot f u 1:2:(dy($2)) i 0 w yerr lt rgb 'blue' title sprintf(text,"Links","erster"), \
     f u 1:3:(dy($2)) i 0 w yerr lt rgb 'red' title sprintf(text,"Rechts","erster"), \
     f u 1:2:(dy($2)) i 1 w yerr lt rgb 'cyan' title sprintf(text,"Links","zweiter"), \
     f u 1:3:(dy($2)) i 1 w yerr lt rgb 'magenta' title sprintf(text,"Rechts","zweiter")

####################################################################################
set output 'grafiken/appasym.tikz'

kappa = 1.3595
dkappa = 0.0061

LR(x,y) = x/y
dLR(x, y) = LR(x,y) * sqrt(1/x + 1/y)

set ylabel '$\kappa_\text{app}$'
plot 'data/apparative_asymmetrie_korr.txt' u 1:(LR($2,$3)):(dLR($2,$3)) w yerr \
    notitle, \
    kappa lt rgb 'blue' title 'Mittelwert', \
    kappa - dkappa lt rgb 'grey' notitle, \
    kappa + dkappa lt rgb 'grey' notitle

####################################################################################
set output 'grafiken/streuung.tikz'

LR2(x,y) = x/(y * kappa)
dLR2(x,y) = LR2(x,y) * sqrt(1/x + 1/y + (dkappa/kappa)**2)

delta = 0.05
LR_fit(x) = kappa * (1 + delta * cos(x))/(1 - delta * cos(x)) + X

fit LR_fit(x) 'data/mottstreuung_korr.txt' u 1:(LR2($2,$3)):(1/dLR2($2,$3)**2) \
                                                             via delta, X 

set ylabel '$\frac{L_\text{korr}}{R_\text{korr} \kappa_\text{app}}$'
plot 'data/mottstreuung_korr.txt' u 1:(LR2($2,$3)):(dLR2($2,$3)) w yerr notitle,\
    LR_fit(x) notitle

####################################################################################
set output 'grafiken/mottasym.tikz'

dd = 0.5

set autoscale x
set xlabel '$d/\unit{\mu m}$'
set ylabel "$\\delta'^{-1}$"

delta_p(x,y,z,w) = (1 + sqrt(LR(z,w)/LR(x,y)))/(1 - sqrt(LR(z,w)/LR(x,y)))
ddelta_p(x,y,z,w) = delta_p(x,y,z,w) * sqrt( \
        LR(x,y)/LR(z,w) * dLR(z,w)**2 + LR(z,w)/LR(x,y) * dLR(x,y)**2 \
        ) / abs(LR(x,y) - LR(z,w))

m = 5
b = 6
f(x) = m * x + b

fit f(x) 'data/mottasymmetrie_korr.txt' u \
     1:(delta_p($2,$3,$4,$5)):(1/ddelta_p($2,$3,$4,$5)**2) via m,b


plot 'data/mottasymmetrie_korr.txt' u \
    1:(delta_p($2,$3,$4,$5)):(0.05):(ddelta_p($2,$3,$4,$5)) w xyerr notitle, \
    f(x) notitle

